buildscript {
    ext.kotlin_version = '1.0.5-2'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.8'
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'idea'

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

version = "0.2.0"
group = "info.brage"
archivesBaseName = "prometheus-integration"

minecraft {
    version = "1.10.2-12.18.2.2179"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_29"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        name 'shadowfacts'
        url 'http://mvn.rx14.co.uk/shadowfacts/'
    }
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.1.0"
//    compile group: "net.shadowfacts", name: "ShadowMC", version: "1.10.2-3.6.0"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    main.kotlin.srcDirs += 'src/main/kotlin'
}

idea {
    module.inheritOutputDirs = true
    project.languageLevel = '1.8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ""
    project {
        id = '254573'
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = 'alpha'
        addGameVersion "1.10.2"
        addGameVersion "Java 8"
        relations {
            requiredLibrary 'shadowfacts-forgelin'
        }
        mainArtifact(shadowJar) {
            displayName = "Prometheus-Integration $project.version"
        }
        addArtifact(sourcesJar) {
            displayName = "Prometheus-Integration $project.version Sources"
        }
//        addArtifact(deobfJar) {
//            displayName = "Literal Ascension $project.version Deobfuscated"
//        }
    }
}

dependencies {
    compile "io.prometheus:simpleclient:0.0.14"
    compile "io.prometheus:simpleclient_servlet:0.0.14"
    compile "io.prometheus:simpleclient_hotspot:0.0.14"
    compile "org.eclipse.jetty:jetty-servlet:8.1.19.v20160209"
}

shadowJar {
    classifier = ""
    relocate "javax.servlet", "info.brage.repack.javax.servlet"
    relocate "io.prometheus", "info.brage.repack.io.prometheus"
    relocate "org.eclipse", "info.brage.repack.org.eclipse"
    relocate "org.jetbrains", "info.brage.repack.org.jetbrains"
}

tasks.build.dependsOn sourceJar, shadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

reobf { shadowJar { mappingType = 'SEARGE' } }
tasks.reobfShadowJar.mustRunAfter shadowJar
afterEvaluate {
	tasks.curseforge254573.dependsOn reobfShadowJar
}
