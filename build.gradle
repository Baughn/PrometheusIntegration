buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
}
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply plugin: 'forge'

version = "1.0.1"
group= "info.brage.minecraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "prometheus-integration"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "run"
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "io.prometheus:simpleclient:0.0.14"
    shade "io.prometheus:simpleclient_servlet:0.0.14"
    shade "io.prometheus:simpleclient_hotspot:0.0.14"
    shade "org.eclipse.jetty:jetty-servlet:8.1.19.v20160209"
}

// Shade the extra libraries we need.
minecraft {
    srgExtra "PK: io/prometheus info/brage/shade/io/prometheus"
    srgExtra "PK: org/eclipse info/brage/shade/org/eclipse"
    srgExtra "PK: javax/servlet info/brage/shade/javax/servlet"
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
